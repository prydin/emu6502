        .ORG    $2000

        ; Physics constants
GRAVITY .EQ     32      ; 1/8 pixels/frame^2
BOUNCE  .EQ     240     ; 15/16 of kinetic energy preserved in bounce

        ; Initial coordinates
XINITL  .EQ     0
XINITM  .EQ     160
XINITH  .EQ     0
YINITL  .EQ     0
YINITH  .EQ     265 * 200 / 2

        ; Sprite tables
SPRSTART .EQ    ((>SPRITE << 8) + <SPRITE) >> 6
SPREND   .EQ    SPRSTART + 30 ; 30 rotations

        ; Zero page variables
VX      .EQ     $B2
VY      .EQ     $B4
XPOS    .EQ     $B6
YPOS    .EQ     $B9

VIC     .EQ     $D000
SPTRS   .EQ     $07f8

        ; Randomize velocities
        LDY     #$00
        LDX     #$00
RANDV   JSR     NEWVELO
        LDA     VX
        STA     VX0,Y
        LDA     VX+1
        STA     VX0+1,Y
        LDA     VY
        STA     VY0,Y
        LDA     VY+1
        STA     VY0+1,Y
        INX
        INX
        INX
        INY
        INY
        CPY     #$10
        BNE     RANDV

        ; Initialize sprites
        LDA     #SPRSTART   ; Load address of sprite 1 data
        LDX     #$00
LOADSP  STA     SPTRS,X
        ADC     #$01
        INX
        CPX     #$08
        BNE     LOADSP
        LDA     #100
        STA     VIC
        STA     VIC+$01

        LDA     #$FF
        STA     VIC+$15     ; Enable all sprites

        ; Initialize interrupts
        SEI                 ; Disable IRQ
        LDA     VIC+$25     ; Enable raster interrupts
        ORA     #$01
        STA     VIC+$25
        LDA     #$30
        STA     VIC+$12
        LDA     VIC+$1A     ; Enable raster IRQ
        ORA     #$01
        STA     VIC+$1A
        LDA     #<FRAME
        STA     $0314
        LDA     #>FRAME
        STA     $0315
        CLI                 ; Enable IRQ
        RTS

        ; Raster IRQ - New frame
FRAME   BIT     VIC+$19     ; Is it for us?
        BMI     DOIRQ
        JMP     $EA31       ; Not for us, give control back to kernal

DOIRQ   STA     ASAVE       ; Save registers
        STX     XSAVE
        STY     YSAVE

        ; Rotate sprites
        LDX     #$00
LOOP1   INC     SPTRS,X
        LDA     SPTRS,X
        CMP     #SPREND
        BNE     CONT
        LDA     #SPRSTART
        STA     SPTRS,X
CONT    INX
        CPX     #$08
        BNE     LOOP1

        LDY     #$00        ; Pointer for everything except X position (because it's 3 bytes)
        LDX     #$00        ; X-position pointer

NXTSPRT LDA     X0,X        ; Load temporary X position variable
        STA     XPOS
        LDA     X0+1,X
        STA     XPOS+1
        LDA     X0+2,X
        STA     XPOS+2
        LDA     Y0,Y        ; Load temporary Y position variable
        STA     YPOS
        LDA     Y0+1,Y
        STA     YPOS+1
        LDA     VX0,Y       ; Load temporary X velocity variable
        STA     VX
        LDA     VX0+1,Y
        STA     VX+1
        LDA     VY0,Y       ; Load temporary Y velocity variable
        STA     VY
        LDA     VY0+1,Y
        STA     VY+1

        ; Physics calculations
        ; Handle vertical bounce
        LDA     VY+1     ; Bounce on downward motion only
        AND     #$80
        BNE     HBOUNCE
        LDA     YPOS+1       ; Check if we've passed the lower bound
        CMP     #230
        BCC     HBOUNCE

        ; Calculate lost momentum in bounce. 1/8 of the momentum is lost in each bounce
        ; This can be expressed as:
        ; v * 7/8 = x - (v/8) = v - (v >> 3)
        LDA     VY
        STA     TEMP1
        LDA     VY+1
        STA     TEMP2
        CMP     #$80        ; Sign extend (Original MSB negative -> carry set)
        ROR     TEMP2       ; 1
        ROR     TEMP1
        CMP     #$80
        ROR     TEMP2       ; 2
        ROR     TEMP1
        CMP     #$80
        ROR     TEMP2       ; 3
        ROR     TEMP1

        ; TEMP2 and TEMP1 now hold Y0/8 high and low byte respectively. Apply damping and flip speed vector.
        ; Take advantage of the fact that -(x*7/8) = (v >>> 3) - v
        SEC
        LDA     TEMP1
        SBC     VY
        STA     VY
        LDA     TEMP2
        SBC     VY+1
        STA     VY+1

        ; Avoid strange effects by setting the LSB to zero if the MSB is zero after the bounce.
        ; This will make the object come to rest.
        BEQ     DONEWV
        CMP     #$FF
        BNE     HBOUNCE
DONEWV  JSR     NEWVELO
        LDA     #XINITL
        STA     XPOS
        LDA     #XINITM
        STA     XPOS+1
        LDA     #XINITH
        STA     XPOS+2
        LDA     #YINITL
        STA     YPOS
        LDA     #YINITH
        STA     YPOS+1

        ; Handle horizontal bounce
HBOUNCE LDA     XPOS+2
        CMP     #$01        ; Right edge bounce
        BCC     LBOUNCE
        LDA     XPOS+1
        CMP     #82
        BCS     DOHBNC      ; Yes we are bouncing!
LBOUNCE LDA     XPOS+2      ; Handle left edge
        BNE     APPLYG
        LDA     XPOS+1
        CMP     #30
        BCS     APPLYG

        ; Calculate lost momentum in bounce. 1/8 of the momentum is lost in each bounce
        ; This can be expressed as:
        ; v * 7/8 = x - (v/8) = v - (v >> 3)
DOHBNC  LDA     VX
        STA     TEMP1
        LDA     VX+1
        STA     TEMP2
        CMP     #$80        ; Sign extend (Original MSB negative -> carry set)
        ROR     TEMP2       ; 1
        ROR     TEMP1
        CMP     #$80
        ROR     TEMP2       ; 2
        ROR     TEMP1
        CMP     #$80
        ROR     TEMP2       ; 3
        ROR     TEMP1

        ; TEMP2 and TEMP1 now hold Y0/8 high and low byte respectively. Apply damping and flip speed vector.
        ; Take advantage of the fact that -(x*7/8) = (v >>> 3) - v
        SEC
        LDA     TEMP1
        SBC     VX
        STA     VX
        LDA     TEMP2
        SBC     VX+1
        STA     VX+1

        ; Apply gravity to velocity vector
APPLYG  CLC
        LDA     VY
        ADC     #GRAVITY
        STA     VY
        LDA     VY+1
        ADC     #$00        ; Handle carry
        STA     VY+1

        ; Update position
        LDA     XPOS          ; Add X-portion of speed vector to position
        CLC
        ADC     VX
        STA     XPOS
        LDA     XPOS+1
        ADC     VX+1
        STA     XPOS+1
        STA     VIC,Y
        LDA     XPOS+2
        BIT     VX+1        ; Sign extend into third byte
        BPL     POS
        ADC     #$FF
        CLC
POS     ADC     #$00        ; Handle carry into third byte
        STA     XPOS+2
        BEQ     CLEARHI     ; Setting or clearing high bit?
        LDA     HIMASK,Y    ; Setting it. Just OR it in
        ORA     VIC+$10
        STA     VIC+$10
        BNE     SETYPOS
CLEARHI LDA     HIMASK,Y    ; Clearing it. And with inverse
        EOR     #$FF
        AND     VIC+$10
        STA     VIC+$10

SETYPOS CLC
        LDA     YPOS         ; Y coord
        ADC     VY
        STA     YPOS
        LDA     YPOS+1
        ADC     VY+1
        STA     YPOS+1
        STA     VIC+1,Y

        ; Write back to non-temporary variables

        STA     Y0+1,Y        ; A should already hold [YPOS+1]
        LDA     XPOS
        STA     X0,X
        LDA     XPOS+1
        STA     X0+1,X
        LDA     XPOS+2
        STA     X0+2,X
        LDA     YPOS
        STA     Y0,Y
        LDA     VX
        STA     VX0,Y
        LDA     VX+1
        STA     VX0+1,Y
        LDA     VY
        STA     VY0,Y
        LDA     VY+1
        STA     VY0+1,Y

        INY                 ; Move to next sprite
        INY
        CPY     #$10        ; Handled all sprites?
        BEQ     END
        INX
        INX
        INX
        JMP     NXTSPRT

END     LDA     ASAVE
        LDX     XSAVE
        LDY     YSAVE
        DEC     VIC+$19     ; Acknowledge interrupt
        JMP     $EA31

        ; Subroutine to get an 8-bit random number
RND8    JSR     RAND
        LDA     SEED0
        EOR     SEED1
        EOR     SEED2
        EOR     SEED3
        RTS

        ; Get random number for high byte of velocities. We limit it to 3 and sign negate randomly
RNDHI   JSR     RND8
        LSR                 ; Put lowest bit into carry
        AND     #$03        ; Use only 2 lowest bits
        BCC     RNDPOS      ; Negate?
        EOR     #$FF        ; Ones complement
RNDPOS  ADC     #$00        ; Twos complement if carry is set. Does nothing otherwise.
        RTS

        ; Subroutine to calculate random velocity vectors
NEWVELO JSR     RND8
        STA     VX
        JSR     RNDHI
        STA     VX+1
        JSR     RND8
        STA     VY
        JSR     RNDHI
        STA     VY+1
        RTS

        .INCLUDE    demos/random.a65

; Temporary variables
ASAVE   .DB     0           ; Acc during IRQ
XSAVE   .DB     0           ; X during IRQ
YSAVE   .DB     0           ; Y during IRQ

TEMP1   .DB     0           ; General purpose junk variable
TEMP2   .DB     0           ; General purpose junk variable
        
X0      .DB     0, 160, 0
X1      .DB     0, 160, 0
X2      .DB     0, 160, 0
X3      .DB     0, 160, 0
X4      .DB     0, 160, 0
X5      .DB     0, 160, 0
X6      .DB     0, 160, 0
X7      .DB     0, 160, 0

Y0      .DW     265 * 200 / 2     ; 16 bit Y coordinates
Y1      .DW     265 * 200 / 2
Y2      .DW     265 * 200 / 2
Y3      .DW     265 * 200 / 2
Y4      .DW     265 * 200 / 2
Y5      .DW     265 * 200 / 2
Y6      .DW     265 * 200 / 2
Y7      .DW     265 * 200 / 2

VX0     .DW     $fc01     ; X portion of speed vectors
VX1     .DW     $0100
VX2     .DW     $0000
VX3     .DW     $0000
VX4     .DW     $0000
VX5     .DW     $0000
VX6     .DW     $0000
VX7     .DW     $0000

VY0     .DW     $0000     ; Y portion of speed vectors
VY1     .DW     $0000
VY2     .DW     $0000
VY3     .DW     $0000
VY4     .DW     $0000
VY5     .DW     $0000
VY6     .DW     $0000
VY7     .DW     $0000

HIMASK  .DB      $01, $01, $02, $02, $04, $04, $08, $08, $10, $10, $20, $20, $40, $40, $80, $80

        .ALIGN  64
SPRITE
 ; 0 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $3f, $00
        .DB     $00, $ff, $c0
        .DB     $03, $f9, $f0
        .DB     $07, $f9, $f8
        .DB     $07, $ff, $e0
        .DB     $0f, $ff, $c0
        .DB     $0f, $ff, $80
        .DB     $0f, $fe, $00
        .DB     $0f, $fc, $00
        .DB     $1f, $fe, $00
        .DB     $0f, $ff, $80
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $f8
        .DB     $03, $ff, $f0
        .DB     $00, $ff, $c0
        .DB     $00, $3f, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 12 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $7d, $80
        .DB     $00, $71, $e0
        .DB     $00, $f1, $c0
        .DB     $03, $f3, $80
        .DB     $07, $ff, $00
        .DB     $07, $fe, $00
        .DB     $07, $fe, $00
        .DB     $07, $fc, $00
        .DB     $07, $fc, $00
        .DB     $07, $ff, $c0
        .DB     $07, $ff, $f8
        .DB     $01, $ff, $f0
        .DB     $01, $ff, $e0
        .DB     $00, $ff, $80
        .DB     $00, $2f, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 24 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $0a, $00
        .DB     $00, $37, $80
        .DB     $00, $c3, $80
        .DB     $00, $e3, $00
        .DB     $01, $e7, $00
        .DB     $03, $ff, $00
        .DB     $03, $fc, $00
        .DB     $07, $fc, $00
        .DB     $07, $fc, $00
        .DB     $07, $ff, $d0
        .DB     $07, $ff, $fc
        .DB     $03, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $00, $ff, $e0
        .DB     $00, $ff, $80
        .DB     $00, $0a, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 36 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $0b, $00
        .DB     $00, $6e, $00
        .DB     $00, $ce, $00
        .DB     $01, $86, $00
        .DB     $01, $ce, $00
        .DB     $03, $fc, $00
        .DB     $03, $fc, $00
        .DB     $07, $fc, $00
        .DB     $07, $fd, $7c
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $f8
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $f0
        .DB     $01, $ff, $c0
        .DB     $00, $ff, $80
        .DB     $00, $34, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 48 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $0c, $00
        .DB     $00, $38, $00
        .DB     $00, $b8, $00
        .DB     $01, $1c, $00
        .DB     $03, $1c, $00
        .DB     $03, $b8, $00
        .DB     $03, $f8, $00
        .DB     $07, $f8, $38
        .DB     $07, $fe, $f0
        .DB     $0f, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $c0
        .DB     $01, $ff, $80
        .DB     $00, $fe, $00
        .DB     $00, $08, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 60 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $10, $00
        .DB     $00, $70, $00
        .DB     $01, $f0, $00
        .DB     $01, $70, $00
        .DB     $02, $18, $00
        .DB     $06, $30, $30
        .DB     $07, $f8, $f0
        .DB     $07, $f0, $f0
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $c0
        .DB     $01, $ff, $00
        .DB     $00, $fe, $00
        .DB     $00, $28, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 72 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $40, $00
        .DB     $00, $e0, $00
        .DB     $00, $f0, $00
        .DB     $02, $f0, $10
        .DB     $02, $30, $70
        .DB     $02, $30, $f0
        .DB     $07, $78, $f0
        .DB     $07, $ff, $f8
        .DB     $03, $ff, $f0
        .DB     $07, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $03, $ff, $e0
        .DB     $03, $ff, $c0
        .DB     $01, $ff, $c0
        .DB     $00, $ff, $00
        .DB     $00, $3e, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 84 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $80, $00
        .DB     $01, $c0, $60
        .DB     $03, $e0, $60
        .DB     $04, $e1, $e0
        .DB     $04, $61, $e0
        .DB     $04, $73, $f0
        .DB     $07, $7f, $f0
        .DB     $07, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $01, $ff, $e0
        .DB     $01, $ff, $e0
        .DB     $01, $ff, $c0
        .DB     $01, $ff, $00
        .DB     $00, $7c, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 96 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $80
        .DB     $03, $01, $c0
        .DB     $03, $81, $e0
        .DB     $03, $e1, $f0
        .DB     $03, $61, $f0
        .DB     $04, $73, $f0
        .DB     $04, $7f, $f0
        .DB     $04, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $c0
        .DB     $03, $ff, $c0
        .DB     $01, $ff, $c0
        .DB     $00, $7f, $c0
        .DB     $00, $1f, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 108 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $01, $00
        .DB     $00, $03, $80
        .DB     $00, $03, $80
        .DB     $04, $03, $e0
        .DB     $07, $83, $e0
        .DB     $07, $c3, $e0
        .DB     $06, $c7, $f0
        .DB     $08, $ff, $f0
        .DB     $04, $7f, $e0
        .DB     $04, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $e0
        .DB     $01, $ff, $e0
        .DB     $01, $ff, $c0
        .DB     $00, $7f, $80
        .DB     $00, $3e, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 120 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $08, $00
        .DB     $00, $0e, $00
        .DB     $00, $0f, $80
        .DB     $00, $07, $80
        .DB     $00, $0f, $c0
        .DB     $0c, $07, $e0
        .DB     $0f, $8f, $e0
        .DB     $0f, $af, $e0
        .DB     $0c, $ff, $e0
        .DB     $08, $ff, $e0
        .DB     $08, $ff, $e0
        .DB     $05, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $c0
        .DB     $00, $ff, $80
        .DB     $00, $7f, $00
        .DB     $00, $14, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 132 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $30, $00
        .DB     $00, $1c, $00
        .DB     $00, $1f, $00
        .DB     $00, $1f, $80
        .DB     $00, $1f, $c0
        .DB     $00, $0f, $c0
        .DB     $00, $0f, $c0
        .DB     $1f, $9f, $e0
        .DB     $0f, $ff, $e0
        .DB     $0c, $ff, $f0
        .DB     $00, $7f, $e0
        .DB     $0c, $ff, $e0
        .DB     $05, $ff, $e0
        .DB     $03, $ff, $c0
        .DB     $01, $ff, $80
        .DB     $00, $7f, $00
        .DB     $00, $10, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 144 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $68, $00
        .DB     $00, $3f, $00
        .DB     $00, $3f, $80
        .DB     $00, $1f, $c0
        .DB     $00, $1f, $c0
        .DB     $00, $0f, $e0
        .DB     $00, $0f, $e0
        .DB     $02, $9f, $f0
        .DB     $1f, $ff, $f0
        .DB     $06, $ff, $f0
        .DB     $0e, $7f, $f0
        .DB     $00, $3f, $f0
        .DB     $06, $7f, $e0
        .DB     $01, $ff, $c0
        .DB     $00, $ff, $80
        .DB     $00, $16, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 156 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $28, $00
        .DB     $00, $fe, $00
        .DB     $00, $ff, $80
        .DB     $00, $3f, $80
        .DB     $00, $3f, $c0
        .DB     $00, $1f, $e0
        .DB     $00, $0f, $e0
        .DB     $00, $1f, $f0
        .DB     $00, $bf, $f0
        .DB     $07, $ff, $f0
        .DB     $1f, $ff, $f0
        .DB     $06, $1f, $e0
        .DB     $06, $3f, $e0
        .DB     $03, $3f, $80
        .DB     $00, $ff, $80
        .DB     $00, $28, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 168 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $ff, $00
        .DB     $03, $ff, $00
        .DB     $01, $ff, $80
        .DB     $00, $7f, $e0
        .DB     $00, $3f, $f0
        .DB     $00, $3f, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $0f, $3f, $f0
        .DB     $07, $1f, $c0
        .DB     $03, $1f, $c0
        .DB     $00, $9f, $80
        .DB     $00, $7a, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 180 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $fc, $00
        .DB     $03, $ff, $00
        .DB     $0f, $ff, $c0
        .DB     $1f, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $01, $ff, $f0
        .DB     $00, $7f, $f8
        .DB     $00, $3f, $f0
        .DB     $00, $7f, $f0
        .DB     $01, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $1f, $9f, $e0
        .DB     $0f, $9f, $c0
        .DB     $03, $ff, $00
        .DB     $00, $fc, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 192 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $7a, $00
        .DB     $00, $ff, $80
        .DB     $03, $ff, $c0
        .DB     $07, $ff, $c0
        .DB     $0f, $ff, $f0
        .DB     $01, $ff, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $3f, $f0
        .DB     $00, $3f, $f0
        .DB     $00, $7f, $f0
        .DB     $00, $e7, $e0
        .DB     $01, $c7, $80
        .DB     $03, $c7, $00
        .DB     $00, $df, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 204 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $28, $00
        .DB     $00, $ff, $80
        .DB     $03, $ff, $80
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $1f, $ff, $f0
        .DB     $05, $ff, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $1f, $e0
        .DB     $00, $7f, $e0
        .DB     $00, $73, $c0
        .DB     $00, $63, $80
        .DB     $00, $e1, $80
        .DB     $00, $f6, $00
        .DB     $00, $28, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 216 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $16, $00
        .DB     $00, $ff, $80
        .DB     $01, $ff, $c0
        .DB     $07, $ff, $e0
        .DB     $03, $ff, $f0
        .DB     $0f, $ff, $f0
        .DB     $07, $ff, $f0
        .DB     $1f, $5f, $f0
        .DB     $00, $1f, $f0
        .DB     $00, $1f, $e0
        .DB     $00, $1f, $e0
        .DB     $00, $39, $c0
        .DB     $00, $30, $c0
        .DB     $00, $39, $80
        .DB     $00, $3b, $00
        .DB     $00, $68, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 228 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $10, $00
        .DB     $00, $7f, $00
        .DB     $01, $ff, $80
        .DB     $03, $ff, $c0
        .DB     $07, $ff, $e0
        .DB     $0f, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $0f, $ff, $f0
        .DB     $0f, $7f, $e0
        .DB     $1c, $1f, $e0
        .DB     $00, $1f, $c0
        .DB     $00, $1d, $c0
        .DB     $00, $38, $c0
        .DB     $00, $38, $80
        .DB     $00, $1d, $00
        .DB     $00, $1c, $00
        .DB     $00, $30, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 240 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $14, $00
        .DB     $00, $7f, $00
        .DB     $00, $ff, $80
        .DB     $03, $ff, $c0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $0f, $ff, $e0
        .DB     $0f, $ff, $e0
        .DB     $0f, $ff, $e0
        .DB     $0f, $0f, $e0
        .DB     $0f, $1f, $e0
        .DB     $0c, $0c, $60
        .DB     $00, $18, $40
        .DB     $00, $0e, $80
        .DB     $00, $0f, $80
        .DB     $00, $0e, $00
        .DB     $00, $08, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 252 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $3e, $00
        .DB     $00, $7f, $80
        .DB     $01, $ff, $c0
        .DB     $01, $ff, $e0
        .DB     $03, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $e0
        .DB     $0f, $ff, $f0
        .DB     $07, $8f, $70
        .DB     $07, $86, $20
        .DB     $07, $06, $20
        .DB     $04, $07, $a0
        .DB     $00, $07, $80
        .DB     $00, $03, $80
        .DB     $00, $01, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 264 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $1f, $00
        .DB     $00, $7f, $c0
        .DB     $01, $ff, $c0
        .DB     $03, $ff, $c0
        .DB     $03, $ff, $c0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $f0
        .DB     $07, $ff, $70
        .DB     $07, $e7, $10
        .DB     $03, $c3, $10
        .DB     $03, $c3, $90
        .DB     $03, $03, $e0
        .DB     $03, $01, $c0
        .DB     $00, $00, $80
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 276 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $7c, $00
        .DB     $01, $ff, $00
        .DB     $01, $ff, $c0
        .DB     $01, $ff, $e0
        .DB     $01, $ff, $e0
        .DB     $03, $ff, $f0
        .DB     $03, $ff, $f0
        .DB     $07, $ff, $90
        .DB     $07, $ff, $10
        .DB     $07, $e7, $10
        .DB     $07, $c3, $60
        .DB     $07, $c3, $e0
        .DB     $03, $c0, $e0
        .DB     $01, $c0, $60
        .DB     $00, $80, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 288 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $3e, $00
        .DB     $00, $ff, $00
        .DB     $01, $ff, $c0
        .DB     $03, $ff, $c0
        .DB     $03, $ff, $e0
        .DB     $03, $ff, $f0
        .DB     $07, $ff, $90
        .DB     $03, $ff, $10
        .DB     $07, $ff, $88
        .DB     $07, $f1, $b0
        .DB     $03, $e1, $f0
        .DB     $03, $e0, $f0
        .DB     $03, $e0, $10
        .DB     $00, $e0, $00
        .DB     $00, $e0, $00
        .DB     $00, $40, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 300 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $28, $00
        .DB     $00, $fe, $00
        .DB     $01, $ff, $00
        .DB     $03, $ff, $c0
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $a0
        .DB     $07, $ff, $10
        .DB     $07, $ff, $10
        .DB     $07, $ff, $30
        .DB     $07, $f5, $f0
        .DB     $07, $f1, $f0
        .DB     $07, $e0, $30
        .DB     $03, $f0, $00
        .DB     $01, $e0, $00
        .DB     $01, $f0, $00
        .DB     $00, $70, $00
        .DB     $00, $10, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 312 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $08, $00
        .DB     $00, $fe, $00
        .DB     $01, $ff, $80
        .DB     $03, $ff, $c0
        .DB     $07, $ff, $a0
        .DB     $07, $ff, $30
        .DB     $07, $fe, $00
        .DB     $0f, $ff, $30
        .DB     $07, $ff, $f0
        .DB     $07, $f9, $f8
        .DB     $03, $f0, $00
        .DB     $03, $f0, $00
        .DB     $03, $f8, $00
        .DB     $01, $f8, $00
        .DB     $00, $f8, $00
        .DB     $00, $38, $00
        .DB     $00, $0c, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 324 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $34, $00
        .DB     $00, $ff, $80
        .DB     $01, $ff, $c0
        .DB     $03, $ff, $30
        .DB     $07, $fe, $00
        .DB     $07, $ff, $38
        .DB     $07, $ff, $b0
        .DB     $07, $ff, $fc
        .DB     $07, $fc, $a0
        .DB     $03, $f8, $00
        .DB     $03, $f8, $00
        .DB     $01, $fc, $00
        .DB     $01, $fc, $00
        .DB     $00, $fe, $00
        .DB     $00, $7e, $00
        .DB     $00, $0b, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 336 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $0a, $00
        .DB     $00, $ff, $80
        .DB     $00, $fe, $60
        .DB     $03, $fe, $30
        .DB     $03, $fc, $30
        .DB     $07, $ff, $fc
        .DB     $07, $ff, $f0
        .DB     $07, $fe, $80
        .DB     $07, $fc, $00
        .DB     $03, $f8, $00
        .DB     $03, $fc, $00
        .DB     $01, $fe, $00
        .DB     $00, $fe, $00
        .DB     $00, $ff, $80
        .DB     $00, $3f, $80
        .DB     $00, $0a, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
        ; 348 degrees rotation
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $2f, $00
        .DB     $00, $fc, $80
        .DB     $01, $fc, $60
        .DB     $01, $fc, $70
        .DB     $07, $fe, $78
        .DB     $07, $ff, $e0
        .DB     $07, $ff, $80
        .DB     $07, $fc, $00
        .DB     $07, $fc, $00
        .DB     $07, $fe, $00
        .DB     $07, $fe, $00
        .DB     $03, $ff, $00
        .DB     $00, $ff, $c0
        .DB     $00, $7f, $e0
        .DB     $00, $7f, $80
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00, $00, $00
        .DB     $00 ; 64 bit boundary padding
